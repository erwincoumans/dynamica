/*
Bullet Continuous Collision Detection and Physics Library Maya Plugin
Copyright (c) 2008 Walt Disney Studios

This software is provided 'as-is', without any express or implied warranty.
In no event will the authors be held liable for any damages arising
from the use of this software.
Permission is granted to anyone to use this software for any purpose,
including commercial applications, and to alter it and redistribute it freely,
subject to the following restrictions:

1. The origin of this software must not be misrepresented; you must
not claim that you wrote the original software. If you use this
software in a product, an acknowledgment in the product documentation
would be appreciated but is not required.
2. Altered source versions must be plainly marked as such, and must
not be misrepresented as being the original software.
3. This notice may not be removed or altered from any source distribution.

written by Michael Baker <michael@mBakr.com>
*/

//dynamicaUITools.mel - dependent on dynamicaUI.mel

global proc dynamicaUI_addTools()
{
	global string $dynamicaUI_window;
	global string $dynamicaUI_tabs;
	global string $dynamicaUI_helpLine;
	global string $dynamicaUI_toolTabLabel = "Tools";
	global string $dynamicaUI_toolTab;

	if ( `window -ex $dynamicaUI_window` )
	{
		string $tabs[] = `tabLayout -q -tl -fpn $dynamicaUI_tabs`;
		int $toolsTabExists = 0;
		
		//check if Tools tab exists, delete if yes, create if no
		for ($t in $tabs)
		{
			if ($t == $dynamicaUI_toolTabLabel)
			{ 
				deleteUI $dynamicaUI_toolTab;
				$toolsTabExists = 1;
				break;
			}
		}
		
		if (!$toolsTabExists)
		{
			$dynamicaUI_toolTab = dynamicaUI_createToolsTab();
			tabLayout -e -tl $dynamicaUI_toolTab $dynamicaUI_toolTabLabel $dynamicaUI_tabs;
		}
		
		showWindow $dynamicaUI_window;
	}
}

global proc string dynamicaUI_createToolsTab()
{
	global string $dynamicaUI_tabs;
	global float $dynamicaUITools_massMax = 10;
	string $mainForm = `columnLayout -adj 1 -p $dynamicaUI_tabs`;
		text -al "left" -bgc 1.0 0.1 0.1 -l "Advanced Dynamica Tools. Warning: some of these features are experimental!";
		
		dynamicaUI_createFrame("Compute Mass", "Assign computed mass (based on volume) to selected rigid bodies.", 0, 0);
			rowLayout -nc 2;
				floatField -pre 3 -w 50 -v $dynamicaUITools_massMax -ann "Maximum mass value"
					-cc "$dynamicaUITools_massMax = `floatField -q -v massMax`" massMax;
				button -l "Compute Mass" -c "dynamicaUITools_computeMass()";
			setParent ..;
		setParent ..;
		
	setParent ..;
	
	return $mainForm;
}

global proc dynamicaUITools_computeMass()
{
	global float $dynamicaUITools_massMax;
	string $sel[] = `ls -sl`;
	string $meshes[];
	string $RBs[];
	string $selTmp[] = `ls -sl`;
	float $obVolume;
	float $minVolume = 100000;
	float $maxVolume = 0;
	float $massMin = 1;
	int $i;

	//filter selection
	select -cl;	
	for ($s in $sel) if (mbIsType($s, "dRigidBody")) select -add $s; //if dRigidBody
	$sel = `ls -sl`;
	
	for ($s in $sel)
	{
		$connectedTransforms = mbGetConnectedTransforms($s);
		if (size($connectedTransforms))
		{
			if (mbIsMesh($connectedTransforms[0]))
			{
				$meshes[size($meshes)] = $connectedTransforms[0];
				$RBs[size($RBs)] = $s;
			}
		}
	}
	
	if (!size($meshes)) 
	{
		select -r $selTmp; //restore original selection
		dynamicaUI_print("No connected meshes found in selection.", 2);
		return;
	}
	
	//we are go!
	dynamicaUI_print("Computing mass for " + size($sel) + " rigid bodies...", 0);
	refresh;
	
	for ($i=0;$i<size($meshes);$i++)
	{
		$obVolume = mbMeshVolume($meshes[$i]);
		float $minVolume = 1;
		float $maxVolume = 100;
		float $massMin = 1;
		float $massMax = $dynamicaUITools_massMax;

		if ($obVolume > $maxVolume) $maxVolume = $obVolume;
		//if ($obVolume < $minVolume) $minVolume = $obVolume;
		float $mass = mbScaleFloat($obVolume, $minVolume, $maxVolume, $massMin, $massMax);
		
		if (mbIsKeyed(mbGetShape($RBs[$i])))
		{
			string $curve = mbGetAnimCurve(mbGetShape($RBs[$i]), "mass"); //get mass animation curve
			if ($curve != "")
			{
				float $key[] = mbGetKeyByIndex($curve, 2); //get the 3rd key data
				setKeyframe -at "mass" -t $key[0] -v $mass $RBs[$i]; //assign computed mass to 3rd key
			}
		}
		else
		{
			$m = `getAttr (mbGetShape($RBs[$i]) + ".mass")`;
			if ($m != 0) setAttr ($RBs[$i] + ".mass") $mass;
		}
	}
	
	dynamicaUI_print("Mass computed for " + size($sel) + " rigid bodies...", 0);
}

//volume computation...
// float $obVolume;
// float $minVolume = 10000;
// float $maxVolume = 0;
// float $massMin = 1;
// float $massMax = 100;
// for ($i = 0; $i < size($selection) / 2; $i++) 
// {
// $obVolume = mbMeshVolume($obSelection[$i]);
// if ($obVolume > $maxVolume) $maxVolume = $obVolume;
// if ($obVolume < $minVolume) $minVolume = $obVolume;
// }
//$obVolume = mbMeshVolume($obSelection[$i]);
//$mass = mbScaleValue($obVolume, $minVolume, $maxVolume, $massMin, $massMax);